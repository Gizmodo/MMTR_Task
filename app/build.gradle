plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id "scabbard.gradle" version "0.5.0"
    id 'androidx.navigation.safeargs'
    id 'io.gitlab.arturbosch.detekt'
}

ext.versionMajor = 1
ext.versionMinor = 1
ext.versionPatch = 0

@SuppressWarnings('GrMethodMayBeStatic')
def generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

def generateVersionName() {
    String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    } else {
        versionProps['BUILD_NUMBER'] = '0'
    }

    def buildNumber = versionProps['BUILD_NUMBER'].toInteger() + 1
    versionProps['BUILD_NUMBER'] = buildNumber.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    return "${versionName}.BN-${buildNumber}"
}

android {
    compileSdk 31
    buildToolsVersion '32.1.0 rc1'

    defaultConfig {
        applicationId "com.example.fragmentvm"
        minSdk 21
        targetSdk 32
        versionCode generateVersionCode()
        versionName generateVersionName()
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
        resources.excludes += '/META-INF/{AL2.0,LGPL2.1}'
    }
}
scabbard {
    enabled false
    failOnError true
    fullBindingGraphValidation true
    qualifiedNames false
    outputFormat "svg"
}
detekt {
    toolVersion = "1.19.0"
    buildUponDefaultConfig = false
    allRules = false
    config = files("$rootDir/detekt/detekt.yml")
    baseline = file("$rootDir/detekt/baseline.xml")
    input = files("src/main/java/com")
    debug = true
    parallel = true
}
dependencies {
    def retrofit_version = "2.9.0"
    def okhttp_version = '5.0.0-alpha.4'
    def glide_version = '4.13.0'
    def dagger_version = "2.40.5"
    def lifecycle_version = "2.4.1"
    def kotlinx_coroutines_version = "1.6.0"
    def datastore_version = "1.0.0"
    def nav_version = "2.4.1"

    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "androidx.datastore:datastore-preferences-rxjava3:$datastore_version"
    implementation "androidx.datastore:datastore:$datastore_version"
    implementation "androidx.datastore:datastore-rxjava3:$datastore_version"
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-runtime:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation 'com.google.android.material:material:1.6.0-alpha02'
    implementation "com.google.code.gson:gson:2.8.9"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "io.reactivex.rxjava3:rxjava:3.1.3"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlinx_coroutines_version"

    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
}
tasks.named("detekt").configure {
    reports {
        // Enable/Disable XML report (default: true)
        xml.required.set(true)
        xml.outputLocation.set(file("build/reports/detekt.xml"))

        // Enable/Disable HTML report (default: true)
        html.required.set(true)
        html.outputLocation.set(file("build/reports/detekt.html"))

        // Enable/Disable TXT report (default: true)
        txt.required.set(true)
        txt.outputLocation.set(file("build/reports/detekt.txt"))

        // Enable/Disable SARIF report (default: false)
        sarif.required.set(true)
        sarif.outputLocation.set(file("build/reports/detekt.sarif"))

        custom {
            // The simple class name of your custom report.
            reportId = "CustomJsonReport"
            outputLocation.set(file("build/reports/detekt.json"))
        }
    }
}